/*
 * Copyright 2008 Udai Gupta, Ralf Joachim
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */


/** 
 *- Castor final query language.
 *- Verified against Castor 1.2 parser and lexer.
 *- Changed naming to be similar to EJB QL.
 *- Changed structure of productions to be similar to EJB QL.
 *- Added some features of EJB QL.
 *- For details refer issue CASTOR-2498. 
 * @author <a href="mailto:mailtoud AT gmail DOT com">Udai Gupta</a>
 * @author <a href="mailto:ralf DOT joachim AT syscon DOT eu">Ralf Joachim</a>
 * @version $Revision: 7121 $ $Date: 2006-04-25 16:09:10 -0600 (Tue, 25 Apr 2006) $
 * @since 1.3
 **/
 
 // ---------------------------------------------------------------------
 
options{
  STATIC = false;
  NODE_EXTENDS = "ExtendNode";
//  DEBUG_PARSER          = true ;
//  DEBUG_TOKEN_MANAGER   = true ;
}

 // ---------------------------------------------------------------------

PARSER_BEGIN(CastorQLParser)
package org.castor.cpa.query.castorql;
    public class CastorQLParser{}
PARSER_END(CastorQLParser)

 // ---------------------------------------------------------------------

SKIP:{ " " | "\r"  | "\t"  | "\n" }

 // ---------------------------------------------------------------------

// Keywords
TOKEN[IGNORE_CASE]:{
    <PLUS:"+">
  | <MINUS:"-">
  | <CONCATE:"||">
  | <MULTIPLY:"*">
  | <DIVIDE:"/">
  | <REMAINDER:"%">
  | <GREATERTHAN:">">
  | <LESSTHAN:"<">
  | <GREATEREQUAL:">=">
  | <LESSEQUAL:"<=">
  | <EQUAL:"=">
  | <NOTEQUAL1:"!=">
  | <NOTEQUAL2:"<>">
  | <IN:"IN">
  | <AS:"AS">
  | <IS:"IS">
  | <OR:"OR">
  | <BY:"BY">
  | <MOD:"MOD">
  | <ABS:"ABS">
  | <AND:"AND">
  | <ASC:"ASC">
  | <NOT:"NOT">
  | <NIL:"NIL">
  | <DESC:"DESC">
  | <NULL:"NULL">
  | <DATE:"DATE">
  | <FROM:"FROM">
  | <TIME:"TIME">
  | <LIKE:"LIKE">
  | <LIMIT:"LIMIT">
  | <ORDER:"ORDER">
  | <WHERE:"WHERE">
  | <SELECT:"SELECT">
  | <OFFSET:"OFFSET">
  | <ESCAPE:"ESCAPE">
  | <BETWEEN:"BETWEEN">
  | <DISTINCT:"DISTINCT">
  | <UNDEFINED:"UNDEFINED">
  | <TIMESTAMP:"TIMESTAMP">
  | <IS_DEFINED:"IS_DEFINED">
  | <IS_UNDEFINED:"IS_UNDEFINED">
}

 // ---------------------------------------------------------------------

TOKEN:{
    <BOOLEAN_LITERAL:<TRUE>  | <FALSE>>
  | <INTEGER_LITERAL:(<DIGIT>)+>
  | <FLOAT_LITERAL:(<DIGIT>)+("."(<DIGIT>)+(("E"  | "e")(<PLUS>  | <MINUS>)?(<DIGIT>)+)?)?>
  | <TIMESTAMP_LITERAL:"'"<DIGIT><DIGIT><DIGIT><DIGIT>"-"<DIGIT><DIGIT>"-"<DIGIT><DIGIT>
                       (" ")*<DIGIT><DIGIT>":"<DIGIT><DIGIT>":"<DIGIT><DIGIT>("."(<DIGIT>)+)?"'">
  | <DATE_LITERAL:"'"<DIGIT><DIGIT><DIGIT><DIGIT>"-"<DIGIT><DIGIT>"-"<DIGIT><DIGIT>"'">
  | <TIME_LITERAL:"'"<DIGIT><DIGIT>":"<DIGIT><DIGIT>":"<DIGIT><DIGIT>("."(<DIGIT>)+)?"'">
  | <STRING_LITERAL:"'" (~["'",":","-"])* ( "\"" (~["'",":","-"])* )* "'">
  | <CHAR_LITERAL:"'" (~["'",":","-"])* ( "''" (~["'",":","-"])* )* "'">
  /* UNICODE IDENTIFIER */
  |<IDENTIFIER: <LETTER>(<LETTER> | <DIGIT>)* >
  /* UNICODE LETTER */
  | <LETTER:  ["\u0024",             // for '$'
               "\u0041"-"\u005a",    // for A-Z
               "\u005f",             // for '_'
               "\u0061"-"\u007a" ]>  // for a-z
             
  /* UNICODE DIGIT */  
  |   <DIGIT:  [ "\u0030"-"\u0039" ]> // for 0-9
  | <#TRUE:"TRUE"  | "true">
  | <#FALSE:"FALSE"  | "false">
}

 // ---------------------------------------------------------------------

SimpleNode castorQL() : {} 
{
  SelectStatement()
  { return jjtThis; }
}
 
void SelectStatement() : {}
{
  SelectClause()
  FromClause()
  (WhereClause())?
  (OrderbyClause())?
  (LimitClause())? 
    
}

void SelectClause() : {  Token t; } 
{
  <SELECT> 
  (t = <DISTINCT> {  jjtThis.setKind(t.kind);  } )?
  SelectExpression()
}

void SelectExpression() : {}
{
  LOOKAHEAD(2) Path() ( <AS> Identifier() )?
  | Identifier() ":" Path()
}

void FromClause() : {}
{
  <FROM> FromDeclaration()
}

void FromDeclaration() : {}
{
  LOOKAHEAD(2) Path() (<AS>)? Identifier() 
  | Identifier() <IN> Path()
}

void WhereClause() : {}
{
  <WHERE> ConditionalExpression()
}

void OrderbyClause() : {}
{
  <ORDER> <BY> OrderbyItem() ( "," OrderbyItem() )*
}

void OrderbyItem() : { Token t; }
{
  Path() ( t = <ASC> { jjtThis.setKind(t.kind); }
  | t = <DESC> { jjtThis.setKind(t.kind); } )?
}

void LimitClause() : {}
{
  <LIMIT>(IntegerLiteral() | Parameter()) 
  ( <OFFSET> (IntegerLiteral() | Parameter()) )?
}

void ConditionalExpression() : {}
{
  ConditionalTerm() ( <OR> ConditionalTerm() )*
}

void ConditionalTerm() : {}
{
  ConditionalFactor() ( <AND> ConditionalFactor() )*
}

void ConditionalFactor() : { Token t; }
{
  (t = <NOT>{ jjtThis.setKind(t.kind); } )?
  ConditionalPrimary()
}

void ConditionalPrimary() : {}
{
  LOOKAHEAD(21) "(" ConditionalExpression() ")"
  | LOOKAHEAD(21) ComparisonExpression()
  | LOOKAHEAD(21) BetweenExpression()
  | LOOKAHEAD(21) LikeExpression()
  | LOOKAHEAD(21) InExpression()
  | LOOKAHEAD(21) NullExpression()
  | LOOKAHEAD(21) UndefinedFunction()
}

void ComparisonExpression() : {}
{
  Expression() ComparisonOperator() Expression()
}

void ComparisonOperator() : { Token t; }
{
  t = <LESSTHAN>{ jjtThis.setKind(t.kind); }
  | t = <LESSEQUAL>{ jjtThis.setKind(t.kind); }
  | t = <GREATEREQUAL>{ jjtThis.setKind(t.kind); }
  | t = <GREATERTHAN>{ jjtThis.setKind(t.kind); }
  | t = <EQUAL>{ jjtThis.setKind(t.kind); }
  | t = <NOTEQUAL1>{ jjtThis.setKind(t.kind); }
  | t = <NOTEQUAL2>{ jjtThis.setKind(t.kind); }
}

void BetweenExpression() : { Token t; }
{
  Expression()
  (t = <NOT> { jjtThis.setKind(t.kind); } )? 
  <BETWEEN> Expression() <AND> Expression()
}

void LikeExpression() : { Token t; }
{
  Expression()
  (t = <NOT>{ jjtThis.setKind(t.kind); } )? 
  <LIKE> ( StringLiteral() | Parameter() )
  ( <ESCAPE> ( StringLiteral() | Parameter() ) )?
}

void InExpression() : { Token t; }
{
  Path()(t = <NOT>{ jjtThis.setKind(t.kind); } )? 
  <IN> "(" InItem() ( "," InItem() )* ")"
}

void InItem() : {}
{
  LOOKAHEAD(2) Path()  | Parameter()   | Literal()
}
void NullExpression() : { Token t; }
{
  ( LOOKAHEAD(2) Path() | Parameter() )
  <IS> ( t = <NOT> { jjtThis.setKind(t.kind); } )?
  <NULL>
}

void UndefinedFunction() : { Token t; }
{
  ( t = <IS_UNDEFINED>{ jjtThis.setKind(t.kind); }
  | t = <IS_DEFINED>{ jjtThis.setKind(t.kind); }
  ) "(" Expression() ")"
}

void Expression():{ Token t; }
{
  Term()(
  (t = <PLUS> { jjtThis.setKind(t.kind); }
  | t = <MINUS> { jjtThis.setKind(t.kind); }
  | t = <CONCATE> { jjtThis.setKind(t.kind);  }
  ) Term() )?
}

void Term():{ Token t; }
{
  Factor()(
  (t = <MULTIPLY> { jjtThis.setKind(t.kind); }
  | t = <DIVIDE> { jjtThis.setKind(t.kind); }
  | t = <MOD> { jjtThis.setKind(t.kind); }
  | t = <REMAINDER> { jjtThis.setKind(t.kind); }
  ) Factor() )?
}

void Factor() : { Token t; }
{
  (t = <PLUS> { jjtThis.setKind(t.kind); }
  | t = <MINUS> { jjtThis.setKind(t.kind); }
  | t = <ABS> { jjtThis.setKind(t.kind); }
  )? Primary()
}

void Primary() : {}
{
  "(" Expression() ")"
  | LOOKAHEAD(2) Parameter()
  | LOOKAHEAD(2) Literal()
  | LOOKAHEAD(2) Function()
  | LOOKAHEAD(2) TimePrimary()
  | LOOKAHEAD(2) DatePrimary()
  | LOOKAHEAD(2) TimestampPrimary()
  | LOOKAHEAD(2) Path()
}

void Function() : {}
{
  Identifier() "(" Expression() ( "," Expression() )* ")"
}

void DatePrimary() : {}
{
  <DATE> DateLiteral()
}

void TimePrimary():{}
{
  <TIME> TimeLiteral()
}

void TimestampPrimary() : {}
{
  <TIMESTAMP> TimestampLiteral()
}

void Literal() : { Token t; }
{
  t = <NIL> { jjtThis.setKind(t.kind);  }
  | t = <UNDEFINED> { jjtThis.setKind(t.kind);  }
  | BooleanLiteral()
  | LOOKAHEAD(2)IntegerLiteral()
  | LOOKAHEAD(2)FloatLiteral()
  | LOOKAHEAD(2)CharLiteral()
  | StringLiteral()
}

void Parameter() : {}
{
  PositionalParameter()
  | NamedParameter()
  | CastorParameter()
}

void PositionalParameter() : {}
{
  "?" IntegerLiteral()
}

void NamedParameter() : {}
{
  ":" Identifier()
}

void CastorParameter() : {}
{
   "$" ( "("Identifier()")" )? IntegerLiteral()
}

void Path() : {} 
{
  Identifier() ( ("."  | "->") Identifier() )*
}

void Identifier() : { Token t; }
{
  t = <IDENTIFIER>{ jjtThis.setText(t.image); }
}

void BooleanLiteral() : { Token t; }
{
  t = <BOOLEAN_LITERAL> { jjtThis.setText(t.image); }
}

void IntegerLiteral() : { Token t; }
{
  t = <INTEGER_LITERAL> { jjtThis.setText(t.image); }
}

void FloatLiteral() : { Token t; }
{
  t = <FLOAT_LITERAL> { jjtThis.setText(t.image); }
}

void CharLiteral() : { Token t; }
{
  t = <CHAR_LITERAL> { jjtThis.setText(t.image); }
}

void StringLiteral() : { Token t; }
{
  t = <STRING_LITERAL> { jjtThis.setText(t.image); }
}

void TimestampLiteral() : { Token t; }
{
  t = <TIMESTAMP_LITERAL> { jjtThis.setText(t.image); }
}

void DateLiteral() : { Token t; }
{
  t = <DATE_LITERAL> { jjtThis.setText(t.image); }
}

void TimeLiteral() : { Token t; }
{
  t = <TIME_LITERAL> { jjtThis.setText(t.image); }
}
